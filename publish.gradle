def aarFile = file("$buildDir/outputs/aar/$project.name-$version" + ".aar")
def aarArtifact = artifacts.add('archives', aarFile) {
    type 'aar'
}

def isReleaseVersion = !version.endsWith('SNAPSHOT')

publishing {

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }

    publications {
        hyperwalletUiPublish(MavenPublication) {
            groupId = hyperwalletGroupId
            artifactId = project.name

            version = version

            artifact(sourcesJar)
            artifact(javadocsJar)
            artifact(aarArtifact)

            pom {
                name = project.mavenName
                description = project.description
                url = 'https://github.com/hyperwallet/hyperwallet-android-ui-sdk'
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        if (it.name in ["commonrepository",
                                        "commonui",
                                        "receiptrepository",
                                        "receiptui",
                                        "transfermethodrepository",
                                        "transfermethodui",
                                        "transferrepository",
                                        "transferui",
                                        "userrepository",
                                        "balancerepository"
                        ]) {
                            dependencyNode.appendNode('groupId', "com.hyperwallet.android.ui")
                        } else {
                            dependencyNode.appendNode('groupId', it.group)
                        }
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'devs'
                        name = 'Hyperwallet Developers'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/hyperwallet/hyperwallet-android-ui-sdk.git'
                    developerConnection = 'scm:git:git://github.com/hyperwallet/hyperwallet-android-ui-sdk.git'
                    url = 'https://github.com/hyperwallet/hyperwallet-android-ui-sdk'
                }
            }
        }
    }
}

tasks.withType(Sign) {
    onlyIf {
        isReleaseVersion && sonatypeUsername?.trim() && sonatypePassword?.trim()
    }
}

signing {
    sign publishing.publications.hyperwalletUiPublish
}

sonarqube {
    properties {
        def libraries = project.android.sdkDirectory.getPath() + "/platforms/android-28/android.jar"
        property "sonar.sources", "src/main/java"
        property "sonar.binaries", "build/intermediates/javac/release/compileReleaseJavaWithJavac/classes/com/hyperwallet/android/ui"
        property "sonar.libraries", libraries
        property "sonar.projectName", "android-ui-sdk-$project.name"
    }
}